/*******************************************************************************
This file is part of the Shellfish UI toolkit.
Copyright (c) 2020 Martin Grimme <martin.grimme@gmail.com>

Permission is hereby granted, free of charge, to any person obtaining a copy of
this software and associated documentation files (the "Software"), to deal in
the Software without restriction, including without limitation the rights to
use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of
the Software, and to permit persons to whom the Software is furnished to do so,
subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS
FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR
COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER
IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
*******************************************************************************/

require "shellfish/ui";

MouseBox {
    id: button

    property icon: ""
    property secondaryIcon: ""
    property text: ""
    property flat: false
    property checked: false
    property labelProfile: Object { }

    property activated: false

    function format(t)
    {
        const pos = t.indexOf("&");
        if (pos === -1)
        {
            return t;
        }
        else
        {
            return t.substr(0, pos) + "_" + t[pos + 1] + "_" + t.substr(pos + 2);
        }
    }

    color: checked || activated || containsMouse ? theme.highlightBackgroundColor
                                                 : flat ? "transparent"
                                                        : theme.secondaryBackgroundColor

    borderWidth: flat ? 0 : 1
    borderRadius: flat ? 0 : theme.borderRadius
    borderColor: theme.borderColor
    layout: "center-row"

    height: theme.itemHeightSmall

    opacity: ancestorsEnabled && enabled ? 1 : 0.6

    canFocus: true

    onClick: () =>
    {
        focus = true;
        activated = true;
    }

    shortcutReceiver.onKeyDown: (ev) =>
    {
        if (ev.altKey)
        {
            const idx = text.indexOf("&");
            if (idx !== -1)
            {
                const key = text[idx + 1].toLowerCase();
                if (ev.key === key)
                {
                    click(ev);
                    ev.accepted = true;
                }
            }
        }
    }

    onKeyDown: (ev) =>
    {
        if (ev.key === " " || ev.key === "Enter")
        {
            click(ev);
            ev.accepted = true;
        }
    }

    Timer {
        id: activateTimer

        interval: 300
        running: button.activated
        repeat: false

        onTimeout: () =>
        {
            button.activated = false;
        }
    }

    Label {
        visible: button.icon !== ""
        marginTop: 2
        marginLeft: 4
        marginRight: 4
        marginBottom: 2
        fontSize: parent.bbox.height * 0.6
        text: button.icon !== "" ? "[icon:" + button.icon + "]" : ""

        profiles: [button.labelProfile]
    }

    Label {
        visible: button.text !== ""
        fillWidth: true
        marginTop: 2
        marginLeft: button.icon !== "" ? 4 : theme.paddingSmall
        marginRight: button.secondaryIcon !== "" ? 4 : theme.paddingSmall
        marginBottom: 2
        overflowBehavior: "ellipsis"
        horizontalAlignment: "center"
        text: button.format(button.text)

        profiles: [button.labelProfile]
    }

    Label {
        visible: button.secondaryIcon !== ""
        marginTop: 2
        marginLeft: 4
        marginRight: 4
        marginBottom: 2
        fontSize: parent.bbox.height * 0.6
        text: button.secondaryIcon !== "" ? "[icon:" + button.secondaryIcon + "]" : ""

        profiles: [button.labelProfile]
    }

    FocusIndicator {
        id: focusIndicator
        visible: button.focus

        /*
        property flashing: false

        property flash: () =>
        {
            if (! flashing)
            {
                flashing = true;
                flashAnim.start();
            }
        }

        visible: button.focus || flashing

        NumberAnimation {
            id: flashAnim

            duration: 500
            from: 1
            to: 0

            onNext: (value) =>
            {
                focusIndicator.opacity = value;
            }

            onFinished: () =>
            {
                focusIndicator.flashing = false;
                focusIndicator.opacity = 1;
            }
        }
        */
    }
}
