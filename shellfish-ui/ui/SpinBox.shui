/*******************************************************************************
This file is part of the Shellfish UI toolkit.
Copyright (c) 2020 - 2022 Martin Grimme <martin.grimme@gmail.com>

Permission is hereby granted, free of charge, to any person obtaining a copy of
this software and associated documentation files (the "Software"), to deal in
the Software without restriction, including without limitation the rights to
use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of
the Software, and to permit persons to whom the Software is furnished to do so,
subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS
FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR
COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER
IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
*******************************************************************************/

require "shellfish/ui";

/**
 * Element representing a UI spinbox for setting a numeric value within a certain range.
 *
 * @memberof ui
 * @name SpinBox
 * @class
 * @extends html.Box
 *
 * @property {number} minValue - (default: `0`) The minimum allowed value.
 * @property {number} maxValue - (default: `100`) The maximum allowed value.
 * @property {number} value - (default: `0`) The current value.
 * @property {number} stepSize - (default: `5`) The step size between value changes when pressing one of the step buttons.
 * @property {template} styleOfBackground - The background style template.
 * @property {template} styleOfLeftEdge - The left edge style template.
 * @property {template} styleOfRightEdge - The right edge style template.
 */
MouseBox {
    id: spinBox

    property minValue: 0
    property maxValue: 100
    property value: 0
    property stepSize: 5

    property styleOfBackground: template Box {
        fillWidth: true
        fillHeight: true

        color: theme.contentBackgroundColor
        borderColor: theme.borderColor
        borderWidth: 1
        borderRadius: theme.borderRadius
    }

    property styleOfLeftEdge: template Button {
        enabled: spinBox.value > spinBox.minValue

        width: bboxHeight
        fillHeight: true

        flat: true
        checked: pressed
        icon: "core-minus"

        repeatAcceleration: 1.5

        onClick: () =>
        {
            spinBox.setValue(spinBox.value - spinBox.stepSize);
        }
    }

    property styleOfRightEdge: template Button {
        enabled: spinBox.value < spinBox.maxValue

        flat: true
        checked: pressed
        icon: "core-plus"

        repeatAcceleration: 1.5

        onClick: () =>
        {
            spinBox.setValue(spinBox.value + spinBox.stepSize);
        }
    }

    /**
     * Sets the current value.
     *
     * @memberof ui.SpinBox.prototype
     * @name setValue
     * @method
     *
     * @param {number} v - The new value.
     */
    function setValue(v)
    {
        const nv = Math.max(minValue, Math.min(maxValue, v));
        if (nv !== value)
        {
            value = nv;
        }
    }

    width: theme.itemWidthLarge
    height: theme.itemHeightSmall

    onMinValueChanged: () => { setValue(value); }
    onMaxValueChanged: () => { setValue(value); }
    onValueChanged: () => { setValue(value); }

    onWheel: ev =>
    {
        if (ev.deltaY < 0)
        {
            spinBox.setValue(spinBox.value - spinBox.stepSize);
        }
        else if (ev.deltaY > 0)
        {
            spinBox.setValue(spinBox.value + spinBox.stepSize);
        }
        ev.accepted = true;
    }

    Loader {
        id: backgroundLoader

        position: "free"
        fillWidth: true
        fillHeight: true
        sourceTemplate: styleOfBackground

        onItemChanged: () =>
        {
            if (item)
            {
                item.add(contentT());
            }
        }

        property contentT: template Box {
            fillWidth: true
            fillHeight: true
            layout: "center-row"

            Loader {
                fillHeight: true
                sourceTemplate: styleOfLeftEdge
            }

            TextEntry {
                property value: spinBox.value

                fillWidth: true
                pattern: "-?[0-9]*"
                text: "" + spinBox.value
                horizontalAlignment: "center"
                borderWidth: 0

                onValueChanged: () =>
                {
                    const t = "" + value;
                    if (t !== text)
                    {
                        text = t;
                    }
                }

                onTextChanged: () =>
                {
                    if (text !== "" && text !== "-")
                    {
                        spinBox.setValue(Number.parseInt(text));
                        if (text !== "" + spinBox.value)
                        {
                            text = "" + spinBox.value;
                        }
                    }
                    else
                    {
                        text = "" + spinBox.minValue;
                    }
                }
            }

            Loader {
                fillHeight: true
                sourceTemplate: styleOfRightEdge
            }
        }
    }
}